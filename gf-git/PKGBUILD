# Maintainer: Arthur HUGEAT <hugeat dot arthur at gmail dot com>
pkgname=gf-git
pkgver=0.17.0.r73.gdc537d0
pkgrel=1
pkgdesc="Gamedev Framework (gf) is a framework to build 2D games in C++14. It is based on SDL and OpenGL ES 2.0, and presents an API that is very similar to the graphics module of SFML with additional features."
arch=('x86_64')
url="https://gamedevframework.github.io/"
license=('Zlib')
depends=('sdl2' 'boost-libs' 'freetype2' 'zlib')
makedepends=('boost' 'cmake' 'git')
provides=('gf')
conflicts=('gf')
replaces=('gf')

source=("git+https://github.com/GamedevFramework/gf")
sha256sums=('SKIP')

pkgver() {
  cd "${srcdir}/gf"
  git describe --long --tags | sed -r 's/^v(.*)/\1/;s/([^-]*-g)/r\1/;s/-/./g'
}

prepare() {
	cd "${srcdir}/gf"
  git checkout develop
	git submodule update --init
}

build() {
  cd "${srcdir}/gf"
  cmake -DGF_USE_EMBEDDED_LIBS=ON \
        -DGF_BUILD_GAMES=OFF \
        -DGF_BUILD_EXAMPLES=OFF \
        -DGF_BUILD_DOCUMENTATION=OFF \
        -DCMAKE_INSTALL_PREFIX=/usr \
        .
  make
}

check() {
  cd "${srcdir}/gf"
  make test
}

package() {
	cd "${srcdir}/gf"

	make DESTDIR="$pkgdir/" install
	mv ${pkgdir}/usr/share/doc/GF ${pkgdir}/usr/share/doc/gf
	install -Dm644 LICENSE.txt ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.txt
}
